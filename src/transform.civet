{ decl } from ./transforms/decl.civet
{ importTransform } from ./transforms/import.civet
{ object } from ./transforms/object.civet
{ requiredTransform } from ./transforms/required.civet
* as ts from @typescript-eslint/typescript-estree
type { Options } from ./cli.civet
MagicString from magic-string

export transforms := new Map [
    ["decl", decl]
    ["import", importTransform]
    ["object", object]
]

export function transform(
  tscode: string
  path: string
  options: Options
): string
    parsed .= ts.parse tscode,
        filePath: path,
        range: true

    code := new MagicString tscode
    enters := options.transform.map (t) => transforms.get(t)!
    ts.simpleTraverse parsed,
        enter: (node, parent) ->
            requiredTransform node, parent, code // ts/civet incompatabilities
            for enter of enters
                enter node, parent, code

    code.appendLeft 0, `// Generated by ts2civet: ${new Date()}\n\n`

    if options.debug
        import("node:fs").then .writeFileSync `${path}.json`, JSON.stringify parsed, null, 2
    code.toString()