meow from meow
{ transform, type Options } from ./transform.civet
{ readFileSync, writeFileSync } from node:fs
{ glob } from glob

cli := meow """
    Usage
      $ ts2civet [input]

    Options
      --help            Show this help message
      --version         Show version
      --(no-)header     Add a ts2civet header to the output (default: true)
      --output, -o      Output file. '-' for stdout, '.' to replace the extension with .civet (default)

    Examples
      $ ts2civet src/**/*.ts
       > Transformed src/a.ts to src/a.civet
       > Transformed src/b.ts to src/b.civet
       > Transformed src/folder/file.ts to src/folder/file.civet

      $ ts2civet src/index.ts -o-
       > console.log "Hello, world!"

      $ ts2civet src/a.ts -o src/civety-a.civet src/b.ts -o. src/import.ts -o-
       > Transformed src/a.ts to src/civety-a.civet
       > Transformed src/b.ts to src/b.civet
       > console.log "Hello, world!"
""",
    // @ts-ignore -- civet rollup bug i think
    importMeta: import.meta,
    flags:
        help:
            type: "boolean"
            default: false
        version:
            type: "boolean"
            default: false
        header:
            type: "boolean"
            default: true
        output:
            type: "string"
            shortFlag: "o"
            default: []
            isMultiple: true
            isRequired: false

if cli.flags.version then cli.showVersion()
if cli.flags.help or cli.input.length is 0 then cli.showHelp()

cli.input = (await.all cli.input.map (pattern) => glob pattern).flat()

if cli.flags.output.length is not 0 and cli.flags.output.length is not cli.input.length
    console.error "Number of output files must match number of input files, or be omitted."
    cli.showHelp()

for [i, input] of cli.input.entries()
    out :=
      if cli.flags.output.length is 0 or cli.flags.output[i] is "."
        input.replace /\.\w+?$/, ".civet"
      else
        cli.flags.output[i]
    source := readFileSync input, "utf8"
    transformed := transform source, input, cli.flags as Options
    if out is "-"
      console.log transformed
    else
      writeFileSync out, transformed
      console.log `Transformed ${input} to ${out}`

