meow from meow
{ transforms, transform, type Options } from ./transform.civet
{ readFileSync, writeFileSync } from node:fs
{ glob } from glob

cli := meow """
    Usage
      $ ts2civet [input]

    Options
      --help            Show this help message
      --version         Show version
      --output, -o      Output file. '-' for stdout, '.' to replace the extension with .civet (default)
      --transform, -t   Transform to apply, can be used multiple times. Defaults to all transforms.

    Transforms:
      + decl .......... Convert `const x = 3` to `x := 3` and `let x = 3` to `x .= 3`
      + import ........ Convert `import { x } from "./y.ts"` to `{ x } from ./y.civet`
      + object ........ Convert explicit objects like `{ x: 3 }` to braceless objects: `x : 3`
      + call .......... Convert explicit calls like `console.log("Hello World")` to implicit calls: `console.log "Hello World"`

    Examples
      $ ts2civet src/**/*.ts
       > Transformed src/a.ts to src/a.civet
       > Transformed src/b.ts to src/b.civet
       > Transformed src/folder/file.ts to src/folder/file.civet

      $ ts2civet src/index.ts -t decl -t import
       > Transformed src/index.ts to src/index.civet

      $ ts2civet src/index.ts -o-
       > console.log "Hello, world!"

      $ ts2civet src/a.ts -o src/civety-a.civet src/b.ts -o. src/import.ts -o-
       > Transformed src/a.ts to src/civety-a.civet
       > Transformed src/b.ts to src/b.civet
       > console.log "Hello, world!"
""",
    importMeta: import.meta,
    flags:
        help:
            type: "boolean"
            default: false
        version:
            type: "boolean"
            default: false
        output:
            type: "string"
            shortFlag: "o"
            default: []
            isMultiple: true
            isRequired: false
        transform:
            type: "string"
            shortFlag: "t"
            choices: [...transforms.keys()]
            default: [...transforms.keys()]
            isMultiple: true
            isRequired: false

if cli.flags.help then cli.showHelp()
if cli.flags.version then cli.showVersion()

cli.input = (await.all cli.input.map (pattern) -> glob pattern).flat()

if cli.flags.output.length is not 0 and cli.flags.output.length is not cli.input.length
    console.error "Number of output files must match number of input files, or be omitted."
    cli.showHelp()

for [i, input] of cli.input.entries()
    out :=
      if cli.flags.output.length is 0 or cli.flags.output[i] is "."
        input.replace /\.\w+?$/, ".civet"
      else
        cli.flags.output[i]
    source := readFileSync input, "utf8"
    transformed := transform source, input, cli.flags as Options
    if out is "-"
      console.log transformed
    else
      writeFileSync out, transformed
      console.log `Transformed ${input} to ${out}`

